// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = "1.5.30"
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.7.0"
        classpath 'com.google.dagger:hilt-android-gradle-plugin:2.42'
        classpath 'androidx.navigation:navigation-safe-args-gradle-plugin:2.5.2'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
    }
}

subprojects {
    if (project.name == "app") {
        apply plugin: "com.android.application"
    } else {
        apply plugin: "com.android.library"
    }
    apply plugin: "kotlin-android"
    apply plugin: 'kotlin-kapt'
    apply plugin: 'dagger.hilt.android.plugin'
    apply plugin: 'androidx.navigation.safeargs.kotlin'

    android {
        compileSdkVersion 33
        buildToolsVersion "30.0.3"

        buildFeatures {
            dataBinding = true
            viewBinding = true
        }

        defaultConfig {
            minSdkVersion 26
            targetSdkVersion 33

            versionCode 1
            versionName "1.0"

            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        kotlinOptions {
            freeCompilerArgs += "-Xuse-experimental=kotlin.Experimental"
            jvmTarget = "1.8"
        }
    }

    apply from: rootDir.path + "/dependencies.gradle"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
